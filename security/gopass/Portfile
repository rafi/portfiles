# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=portfile:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
github.setup        cortex gopass 5ef3ce344c6dfe3a9f387afe1600f984d2497247

version             20170302
categories          devel
platforms           darwin
license             MIT

maintainers         gmail.com:justrafi
description         Gopass: A simple password-store GUI
long_description    ${name}: ${description}

supported_archs     x86_64 i386
checksums           ${name}-${git.branch}.tar.gz \
                    rmd160  9ac521fa95b918e8694454a99bcf0e77e6c22068 \
                    sha256  8490e7cab92b06e5e2c4486359ac6d9bba487c3bb94f0222f984d078b9daa45e

depends_lib     port:qt5 port:gpgme port:qt5-qtquickcontrols
depends_build   port:go
use_configure   no
dist_subdir     go

# Utility for go-vendoring codes
set go.vendors {}

# go.vendors-append name1 ver1 name2 ver2...
proc go.vendors-append {args} {
    global go.vendors

    foreach {imp_name vers} ${args} {
        set vlist [split ${imp_name} /]

        set vdomain [lindex ${vlist} 0]
        set vuser [lindex ${vlist} 1]
        set vname [lindex ${vlist} 2]

        # NOTE: now expects github.com or golang.org
        switch -exact ${vdomain} {
            github.com { set ghuser ${vuser} }
            golang.org { set ghuser golang }
        }

        set fname ${ghuser}-${vname}
        lappend go.vendors [list ${fname} ${imp_name} ${vers}]

        global ${vname}.version
        set ${vname}.version ${vers}

        master_sites-append https://github.com/${ghuser}/${vname}/tarball/${vers}:${fname}
        distfiles-append    ${fname}-${vers}.tar.gz:${fname}
    }
}

# dependencies to build.
go.vendors-append   golang.org/x/crypto           728b753d0135da6801d45a38e6f43ff55779c5c2 \
                    github.com/atotto/clipboard   bb272b845f1112e10117e3e45ce39f690c0001ad \
                    github.com/cortex/gpgme       8adc29782c63580d1733b463abe4a20405e80d3f \
                    github.com/rjeczalik/notify   41a86457b5eea072a7e6f40bbfaf94c8df41d3aa \
                    github.com/limetext/qml-go    e895a291f2aa9d05ba936cab85534827b2f17144


checksums-append    golang-crypto-${crypto.version}.tar.gz \
                        md5     a518ef106e99e1b6a9027ea929e0a829 \
                        sha1    3ced48bae2fb261e7e4e8b0b67f173b9806bb898 \
                        sha256  12d41b541ca9ef48e468a26f9b9564f8c244341068a535a4b03313ef197566f4 \
                    atotto-clipboard-${clipboard.version}.tar.gz \
                        md5     b9e7279d679be18e3a57a78050fd6394 \
                        sha1    8b98cf18e86dd2b3dbe56856b1752ce6ca04b448 \
                        sha256  a481dc4dd08a3b3e51743a7f217e3e0b382b8b1a20b72dcf3a92b10b145ad31a \
                    cortex-gpgme-${gpgme.version}.tar.gz \
                        md5     d993bf5c859f026eb431bf7ce3621e04 \
                        sha1    d7c730289293655c5d692daf4785da3c053ab477 \
                        sha256  61a36845545221d8ca07674da06a5db57f00219ebeadd5747f79d264a43679df \
                    rjeczalik-notify-${notify.version}.tar.gz \
                        md5     a7110398d7e84b0a043dce8ecc76db2d \
                        sha1    5616a26cac34f260e62e3f7659b5bec8525290a4 \
                        sha256  f275db385675c5108d626cb8ce4c8fc9ff7b808d56fad4b0a6a9f42f4b27dfcb \
                    limetext-qml-go-${qml-go.version}.tar.gz \
                        md5     8a1b5945f76a6d00f5ffb4cbb8564480 \
                        sha1    6d4e66ba8de47e7da9553d9903be851cfe85e051 \
                        sha256  04d7ee38ae98ff468b2da4ce1055f4f1fcf750e395712ab45eb9b8c4cdcab946

set gopath      ${workpath}/gopath
set gosrc       ${gopath}/src
post-extract {
    file mkdir ${gosrc}/github.com/${github.author}
    ln -s ${worksrcpath} ${gosrc}/github.com/${github.author}/${github.project}

    reinplace -E {s|/proglottis/|/cortex/|g} ${worksrcpath}/keyinfo.go
    reinplace -E {s|/proglottis/|/cortex/|g} ${worksrcpath}/pass.go

    foreach vlist ${go.vendors} {
        set fname [lindex ${vlist} 0]
        set imp_name [lindex ${vlist} 1]
        file mkdir ${gosrc}/[file dirname ${imp_name}]
        move [glob ${workpath}/${fname}-*] ${gosrc}/${imp_name}
    }
}

build.cmd       go
build.target    build
build.args
build.env       GOPATH="${gopath}" \
                CGO_CFLAGS="-I${prefix}/include" \
                CGO_LDFLAGS="-L${prefix}/lib"

destroot {
    xinstall -d ${destroot}${prefix}/bin
    xinstall -m 755 ${worksrcpath}/${name}-${git.branch} ${destroot}${prefix}/bin/${name}
}
